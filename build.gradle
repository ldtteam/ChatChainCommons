buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
  }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: "com.jfrog.artifactory"

group 'co.chatchain'
version = System.getenv()['BUILD_NUMBER'] != null ? System.getenv()['BUILD_NUMBER'] : System.getenv()['TRAVIS_BUILD_NUMBER']
archivesBaseName = 'commons'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    //compile('org.java-websocket:Java-WebSocket:1.3.4')

    compile('org.json:json:20160810')
    
    compile('org.slf4j:slf4j-jdk14:1.7.25')

    compile 'com.microsoft.signalr:signalr:1.0.2'
}

idea {
    module {
        inheritOutputDirs = true
    }
}

jar {
    from sourceSets.main.output

    manifest {
        attributes([
                'Maven-Artifact'    : "${project.group}:${project.archivesBaseName}:${project.version}",
                'Specification-Title' : "commons",
                'Specification-Vendor' : "chatchain",
                'Specification-Version' : "1",
                'Implementation-Title' : project.name,
                'Implementation-Version' : "${version}",
                'Implementation-Vendor' : "chatchain",
                'Implementation-Timestamp' : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

shadowJar {
    from sourceSets.main.output

    relocate('org.slf4j', 'co.chatchain.org.slf4j')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives shadowJar
}

//Define what to publish and how.
publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId "commons"
            artifact sourcesJar
            artifact shadowJar

            from components.java // original java sources jar

            pom {
                url = 'https://github.com/ldtteam/ChatChainCommons'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE Version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'AshersLab'
                        name = 'Asher'
                        email = 'asherslab@chatchain.co'
                    }
                }
                contributors {
                    contributor {
                        id = 'ldtteam'
                        name = 'Let\'s Develop Together - Team'
                        email = 'info@ldtteam.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ldtteam/chatchaincommons.git'
                    developerConnection = 'scm:git:ssh://github.com/ldtteam/chatchaincommons.git'
                    url = 'https://github.com/ldtteam/ChatChainCommons'
                }
            }
        }
    }
}

//Notify artifactory of what to publish.
artifactory {
    publish {
        defaults {
            publications('maven')
            publishBuildInfo = true   //Publish build-info to Artifactory (true by default)
            publishArtifacts = true   //Publish artifacts to Artifactory (true by default)
            publishPom = true   //Publish generated POM files to Artifactory (true by default).
        }
    }
}
